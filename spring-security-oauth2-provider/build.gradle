import grails.doc.DocPublisher

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:$grailsVersion"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.2"
}

version "3.1.1-SNAPSHOT"
group "org.grails.plugins"

//apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.grails-doc"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    provided "org.grails:grails-core"
    provided "org.grails:grails-web-boot"
    profile "org.grails.profiles:plugin"
    provided "org.grails:grails-plugin-domain-class"
    provided "org.grails:grails-plugin-services"
    provided "org.grails.plugins:gsp"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    compile 'org.grails.plugins:spring-security-core:3.3.0.M1'

    compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.9.RELEASE', {
        [
                "spring-beans",
                "spring-core",
                "spring-context",
                "spring-aop",
                "spring-jdbc",
                "spring-webmvc",
                "spring-security-core",
                "spring-security-config",
                "spring-security-web",
                "spring-tx",
                "commons-codec"
        ].each { exclude module: it }
    }

    compile "com.fasterxml.jackson.core:jackson-databind:2.4.1.3"
    console "org.grails:grails-console"
}

// Source: https://github.com/grails/grails-gradle-plugin/blob/master/gradle/docs.gradle
task gdocs() {
    group 'Documentation'
    description 'Generate Grails Doc style documentation'
    doLast {
        def srcDocs = file("src/docs")
        def outputDir = file("$buildDir/docs/manual")
        def publisher = new DocPublisher(srcDocs, outputDir)
        publisher.with {
            ant = project.ant
            title = 'Grails OAuth 2 Provider Plugin'
            subtitle = 'Grails OAuth2 Provider Plugin'
            version = project.version
            authors = 'Brian Saville, Bobby Vandiver, Roy Willemse'
            license = 'The Apache Software License, Version 2.0'
            copyright = ''
            footer = ''
            engineProperties = [:]
        }
        publisher.publish()
    }
}

grailsPublish {
    //user = 'user'  // Not required if you defined BINTRAY_USER var
    //key = 'key'    // Not required if you defined BINTRAY_KEY var
    userOrg = 'bluesliverx'
    repo = 'grails-plugins'
    githubSlug = 'bluesliverx/grails-spring-security-oauth2-provider'
    license {
        name = 'Apache-2.0'
    }
    title = "spring-security-oauth2-provider"
    desc = "Grails OAuth 2 Provider Plugin"
    developers = [
        bluesliverx:"Brian Saville", 
        'bobby-vandiver':'Bobby Vandiver',
        dynamind:'Roy Willemse'
    ]
    issueTrackerUrl = 'http://github.com/bluesliverx/grails-spring-security-oauth2-provider/issues'
    vcsUrl = 'http://github.com/bluesliverx/grails-spring-security-oauth2-provider'
}
